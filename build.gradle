plugins {
    id 'net.researchgate.release' version '2.4.1'
    id "com.jfrog.bintray" version "1.7"
}

apply plugin: 'java'
apply from: 'release-lib.gradle'
apply from: 'quality.gradle'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
    maven { url "http://dl.bintray.com/epam/reportportal" }
    maven { url "https://jitpack.io" }

}

dependencies {
    compile('com.epam.reportportal:client-java:4.0.6') {
        ext.compileScope = true
    }
    compile('com.epam.reportportal:commons-model:4.0.4') {
        ext.compileScope = true
    }



    compileOnly 'info.cukes:gherkin:2.12.2'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

publishing.publications.all {
    pom.withXml {
        asNode().dependencies.'*'.findAll() {
            it.scope.text() == 'runtime' && (project.configurations.compile.allDependencies.find { dep ->
                dep.name == it.artifactId.text() && dep.hasProperty('compileScope') && dep.compileScope
            })
        }.each {
            it.scope*.value = 'compile'
        }
    }
}
