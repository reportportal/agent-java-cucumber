plugins {
    id 'net.researchgate.release' version '2.8.0'
    id "com.jfrog.bintray" version "1.8.4"
}

apply plugin: 'java'
apply from: 'release-lib.gradle'
apply from: 'quality.gradle'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    jcenter()
    maven { url "http://dl.bintray.com/epam/reportportal" }
    maven { url "https://jitpack.io" }

}

dependencies {
    compile('com.epam.reportportal:client-java:4.0.19') {
        ext.compileScope = true
    }
    compile('com.epam.reportportal:commons-model:4.2.0') {
        ext.compileScope = true
    }

    compileOnly 'info.cukes:gherkin:2.12.2'
}

publishing.publications.all {
    pom.withXml {
        asNode().dependencies.'*'.findAll() {
            it.scope.text() == 'runtime' && (project.configurations.compile.allDependencies.find { dep ->
                dep.name == it.artifactId.text() && dep.hasProperty('compileScope') && dep.compileScope
            })
        }.each {
            it.scope*.value = 'compile'
        }
    }
}

release {
    git {
        requireBranch = 'v4-hotfix'
    }
}
