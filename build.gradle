apply plugin: 'java'

project.ext.releaseMode = project.hasProperty('releaseMode')

def branch = releaseMode ? '5.0.0' : 'develop'
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/build-quality.gradle"
apply from: "https://raw.githubusercontent.com/reportportal/gradle-scripts/$branch/release-commons.gradle"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    releaseMode ? maven { url "http://dl.bintray.com/epam/reportportal" } : maven { url "https://jitpack.io" }
}

dependencies {
    if (releaseMode) {
        compile 'com.epam.reportportal:client-java:5.0.0-BETA-8'
        compile 'com.epam.reportportal:commons-model:5.0.0'
    } else {
        compile 'com.github.reportportal:client-java:20f5674'
        compile 'com.github.reportportal:commons-model:feec39f3'
    }
    compileOnly 'info.cukes:gherkin:2.12.2'
}

wrapper {
    gradleVersion = '5.4.1'
}

task writeAgentProperties(type: WriteProperties) {
    outputFile = file("${project.projectDir}/src/main/resources/agent.properties")
    property('agent.name', project.name)
    property('agent.version', project.version)
}

compileJava.dependsOn writeAgentProperties
afterReleaseBuild.dependsOn bintrayUpload
